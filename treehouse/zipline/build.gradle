apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.zipline'

kotlin {
    iosArm64()
    iosX64()
    iosSimulatorArm64()

    jvm()

    js(IR) {
        browser()
        binaries.executable()
    }

    sourceSets {
        commonMain {
            dependencies {
                api libs.redwood.treehouse
                api libs.redwood.treehouse.lazylayout.api
                implementation project(":treehouse:componentbox")
            }
        }

        jsMain {
            dependencies {
                api libs.redwood.treehouse.guest
                api project(":treehouse:schema:compose:protocol")
                implementation project(":treehouse:presenter")
            }
        }

        iosMain {
            dependsOn(commonMain)
        }
        targets.all { target ->
            // Some Kotlin targets do not have this property, but native ones always will.
            if (target.hasProperty('platformType') && target.platformType.name == "native") {
                target.compilations.main.defaultSourceSet.dependsOn(sourceSets.iosMain)
            }
        }
    }
}

zipline {
    mainFunction = 'com.dropbox.pokedex.treehouse.zipline.preparePresenters'
}